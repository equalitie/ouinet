apply plugin: 'com.android.library'

def properties = new Properties()
file("$ouinet_properties").withInputStream { properties.load(it) }
def ouinetVersionName = properties.getProperty("versionName")
def buildId = properties.getProperty("buildId")

android {
    compileSdkVersion 26
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName ouinetVersionName
        buildConfigField("String", "BUILD_ID", "\"${buildId}\"")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DBOOST_INCLUDEDIR=$boost_includedir",
                          "-DANDROID_STL='c++_shared'",
                          "-DOUINET_CLIENTLIB_PATH=$ouinet_clientlib_path"
                cppFlags "-std=c++14 -frtti -fexceptions"
            }
        }
        ndk {
            abiFilters "$android_abi"
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["$libdir"]
            assets.srcDirs = ["$assetsdir"]
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            buildStagingDirectory (new File(project.buildDir, "../build-cmake"))
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        disable 'UnsafeNativeCodeLocation' // Disable warning about obfsproxy executable in assets
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    testImplementation "junit:junit:4.12"
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation 'org.powermock:powermock-core:2.0.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    testImplementation 'com.android.support.test:rules:1.0.2'
    testImplementation 'com.android.support:support-annotations:28.0.0'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'org.assertj:assertj-core:3.11.1'
}
